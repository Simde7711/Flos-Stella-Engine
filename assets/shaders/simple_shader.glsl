// @Main shader

#version 450

#include "debug.glsl"

#ifdef VERTEX_SHADER
    layout(location = 0) in vec3 position;
    layout(location = 1) in vec3 color;
    layout(location = 2) in vec3 normal;
    layout(location = 3) in vec2 uv;

    layout(location = 0) out vec3 fragColor;
    layout(location = 1) out vec3 fragPosWorld;
    layout(location = 2) out vec3 fragNormalWorld;

    struct PointLight 
    {
        vec4 position; // ignore w
        vec4 color; // w is intensity
    };

    layout(set = 0, binding = 0) uniform GlobalUno
    {
        mat4 projection;
        mat4 view;
        mat4 inverseView;
        vec4 ambientLightColor;
        PointLight pointLights[10];
        int numLights;
    } globalUno;

    layout(push_constant) uniform Push
    {
        mat4 modelMatrix;
        mat4 normalMatrix;
    } push;


    void main()
    {
        vec4 positionWorld = push.modelMatrix * vec4(position, 1.0);
        gl_Position = globalUno.projection * globalUno.view * positionWorld;
        fragNormalWorld = normalize(mat3(push.normalMatrix) * normal);
        fragPosWorld = positionWorld.xyz;
        fragColor = color;
    }
#endif

#ifdef FRAGMENT_SHADER
    layout(location = 0) in vec3 fragColor;
    layout(location = 1) in vec3 fragPosWorld;
    layout(location = 2) in vec3 fragNormalWorld;

    layout(location = 0) out vec4 outColor;

    struct PointLight 
    {
        vec4 position; // ignore w
        vec4 color; // w is intensity
    };

    layout(set = 0, binding = 0) uniform GlobalUno
    {
        mat4 projection;
        mat4 view;
        mat4 inverseView;
        vec4 ambientLightColor;
        PointLight pointLights[10];
        int numLights;
    } globalUno;

    layout(push_constant) uniform Push
    {
        mat4 modelMatrix;
        mat4 normalMatrix;
    } push;

    void main()
    {
        vec3 diffuseLight = globalUno.ambientLightColor.xyz * globalUno.ambientLightColor.w;
        vec3 specularLight = vec3(0.0);
        vec3 surfaceNormal = normalize(fragNormalWorld);

        vec3 cameraPosWorld = globalUno.inverseView[3].xyz;
        vec3 viewDirection = normalize(cameraPosWorld - fragPosWorld);

        for (int i = 0; i < globalUno.numLights; i++)
        {
            PointLight light = globalUno.pointLights[i];
            vec3 directionToLight = light.position.xyz - fragPosWorld;
            float attenuation = 1.0 / dot(directionToLight, directionToLight);
            directionToLight = normalize(directionToLight);
            float cosAngIncidence = max(dot(surfaceNormal, directionToLight), 0);
            vec3 intensity = light.color.xyz * light.color.w * attenuation;

            diffuseLight += intensity * cosAngIncidence;

            vec3 halfAngle = normalize(directionToLight + viewDirection);
            float blinnTerm = dot(surfaceNormal, halfAngle);
            blinnTerm = clamp(blinnTerm, 0, 1);
            blinnTerm = pow(blinnTerm, 532.0);
            specularLight += intensity * blinnTerm;
        }

        outColor = vec4((diffuseLight * fragColor + specularLight * fragColor) * fragColor, 1.0);
    }   
#endif